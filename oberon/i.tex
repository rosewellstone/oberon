\chapter{Oberon Programming Language}\let\thefootnote\relax% to add footnote without the annoying no
\footnotetext{Revision 1.10.2013 / 3.5.2016}
\begin{quotation}
  Make it as simple as possible, but not simpler.
  \begin{flushright}
    -- A. Einstein
  \end{flushright}
\end{quotation}

\section{Introduction}
Oberon is a general-purpose programming language that evolved from Modula-2. Its principal new
feature is the concept of \emph{type extension}. It permits the construction of new data types
on the basis of existing ones and to relate them.

This is not intended as a programmer's tutorial. It is intentionally kept concise. Its function
is to serve as a reference for programmers, implementors, and manual writers. What remains
unsaid is mostly intentionally left so, either because it is derivable from stated rules of the
language, or because it would unnecessarily restrict the freedom of implementors.

This document describes the language defined in 1988/90 as revised in 2007/16.

\subsection{Syntax}
A language is an infinite set of sentences, namely the sentences well formed according to its
syntax. In Oberon, these sentences are called \emph{compilation units}. Each unit is a finite
sequence of symbols from a finite \emph{vocabulary}. The vocabulary of Oberon consists of
identifiers, numbers, strings, operators, delimiters, and comments. They are called \emph{lexical
symbols} and are composed of sequences of characters. (Note the distinction between symbols and
characters.)

To describe the syntax, an \emph{Extended Backus-Naur Formalism} (EBNF) is used. Brackets
\verb|[| and \verb|]| denote optionality of the enclosed sentential form, and braces \verb|{| and
\verb|}| denote its repetition (possibly zero times). Syntactic entities (non-terminal symbols)
are denoted by English words expressing their intuitive meaning. Symbols of the language vocabulary
(terminal symbols) are denoted by strings enclosed in quote marks or by words in capital letters.

\subsection{Vocabulary}
The following lexical rules must be observed when composing symbols. Blanks and line breaks must
not occur within symbols (except in comments, or blanks in strings). They are ignored unless
they are essential to separate 2 consecutive symbols. Symbols are case-sensitive.

\subsubsection{Identifiers}
Sequences of letters and digits. The 1st must be a letter.
\begin{verbatim}
  digit  = 0-9
  letter = A-Z | a-z
  id     = letter {letter | digit}
\end{verbatim}
Examples:
\begin{verbatim}
  x  scan  Oberon  GetSymbol  firstLetter
\end{verbatim}

\subsubsection{Numbers}
(Unsigned) integers or real numbers:
\begin{itemize}
  \item Integers are sequences of digits and may be followed by a suffix letter \verb|H|, which
    indicates hexadecimal representation. No suffix represents the default decimal.
  \item Real numbers always contain the decimal point. Optionally, it may also contain a decimal
    scale factor: leaded by a \verb|E|, which is pronounced as "times ten to the power of".
\end{itemize}
\begin{verbatim}
  dec    = digit {digit}
  hexdig = digit | A-F
  hex    = digit {hexdig}
  int    = dec | hex H
  sign   = + | -
  real   = [sign] dec . {digit} [E [sign] dec]
  number = int | real
\end{verbatim}
Examples:
\begin{verbatim}
  1987         100H = 256
  -12.3     4.567E8 = 456700000
\end{verbatim}

\subsubsection{Strings}
Sequences of characters enclosed in a pair of quote marks ("). Strings cannot contain
any delimiting quote mark. Alternatively, a single-character string may be specified
by the ordinal number of the character in hexadecimal notation followed by an \verb|X|.
The number of characters in a string is called the \emph{length} of the string.
\begin{verbatim}
  string = " {char} " | hex X
\end{verbatim}
Examples:
\begin{verbatim}
  "OBERON"    "Don't worry!"    22X
\end{verbatim}

\subsubsection{Operators \& Delimiters}
Special characters, character pairs, or \emph{reserved words} listed below. Reserved words
consist exclusively of capital letters and cannot be used in the role of identifiers.
\begin{verbatim}
  (  )   + - * /   <  = >    . , ;  :  :=  ..
  [  ]   & | ~ ^   <= # >=   ARRAY    IMPORT
  {  }     DIV      CASE     BEGIN    MODULE
           END      ELSE     CONST    RECORD
  BY IS    FOR      PROC     ELSIF    REPEAT
  DO OF    MOD      THEN     FALSE    RETURN
  IF OR    NIL      TRUE     UNTIL
  IN TO    VAR      TYPE     WHILE    POINTER
\end{verbatim}

\subsubsection{Comments}
Arbitrary character sequences opened by the bracket \verb|(*| and closed by \verb|*)|,
which may NOT be nested. They may be inserted between any 2 symbols in a program and do
not affect its meaning.

\section{Declarations}
\label{sec:DS}
Every identifier occurring in a program must be introduced by a declaration, unless it is
a predefined identifier. Declarations also serve to specify certain permanent properties
of an object, such as whether it is a constant, a type, a variable, or a procedure.

The identifier is then used to refer to the associated object. This is possible in those
parts of a program only which are within the scope of the declaration. No identifier may
denote more than one object within a given scope. The scope extends textually from the
point of the declaration to the end of the block (procedure or module) to which the
declaration belongs and hence to which the object is local.

In its declaration, an identifier in the module's scope may be followed by an export mark
(*) to indicate that it be exported from its declaring module. In this case, the identifier
may be used in other modules, if they import the declaring module. The identifier is then
prefixed by the identifier designating its module (see \ref{sec:mod}). The prefix and the
identifier are separated by a period (.) and together are called a \emph{Qualified IDentifier}.
\begin{verbatim}
  qid = [id.]id
  xid = id[*]
\end{verbatim}

The following identifiers are predefined; they are defined in \ref{sub:pre} or \ref{sub:ppro}:
\begin{verbatim}
  ABS     FLT     NEW     SET     INCL    FLOOR
  ASR     INC     ODD     BYTE    PACK    ASSERT 
  CHR     LEN     ORD     CHAR    REAL    BOOL
  DEC     LSL     ROR     EXCL    UNPK    INT
\end{verbatim}

\subsection{Constant}
A constant declaration associates an identifier with a constant value.
\begin{verbatim}
  const = xid=expr
\end{verbatim}

A constant expression can be evaluated by a \emph{mere textual scan} without actually executing
the program. Its operands (see \ref{sec:expr}) are constants. Examples of constant declarations:
\begin{verbatim}
  N     = 100
  limit = 2*N -1
  all   = {0 .. WordSize-1}
  name  = "Oberon"
\end{verbatim}

\subsection{Type}
\label{sub:typ}
A data type determines the set of values which variables of that type may assume, and the
operators that are applicable. A type declaration is used to associate an identifier with
a type. The types define the structure of variables of this type and, by implication, the
operators that are applicable to components. There are 2 different data structures, namely
\emph{arrays} and \emph{records}, with different component selectors.
\begin{verbatim}
  typdef = xid = type
  type = pre | arr | rec | ptr | pro | fun
  pre = BOOL | BYTE | CHAR | INT | REAL | SET
\end{verbatim}
Examples:
\begin{verbatim}
  Table = ARRAY N OF REAL
  Tree  = POINTER TO Node
  Node  = RECORD key: INT;
            left, right: Tree
          END
 
  CenterNode = RECORD (Node)
                 name: ARRAY 32 OF CHAR;
                 subnode: Tree
               END
  Function   = PROC(x: INT): INT
\end{verbatim}

\subsubsection{Basic Types}
\label{sub:pre}
The following basic types are denoted by predeclared identifiers. The associated operators
are defined in \ref{sub:op}, and the predeclared function procedures in \ref{sub:ppro}.
The values of a given basic type are the following:
\begin{verbatim}
  BOOL TRUE and FALSE
  BYTE integers between 0 and 255
  CHAR characters in a standard charset
  INT  integers
  REAL real numbers
  SET  sets of integers between 0 and an
            implementation-dependent max
\end{verbatim}
\verb|BYTE| is compatible with \verb|INT|, and vice-versa.

\subsubsection{ARRAY}
Structure consisting of a fixed number (\emph{length}) of elements which are all of the
same type (\emph{element type}), and designated by indices between $0$ and $length - 1$:
\begin{verbatim}
  arr = ARRAY len{, len} OF type
\end{verbatim}
Declaration
\begin{verbatim}
  ARRAY N0, N1, ..., Nk OF T
\end{verbatim}
is an abbreviation of
\begin{verbatim}
  ARRAY N0 OF
    ARRAY N1 OF
      ...
        ARRAY Nk OF T
\end{verbatim}
Examples:
\begin{verbatim}
  ARRAY N OF INT
  ARRAY 10, 20 OF REAL
\end{verbatim}

\subsubsection{RECORD}
Structure consisting of a fixed number of elements of any possible type, specifying for
each element (\emph{field}) its type and identifier denoting the field, whose scope is
the record itself, but is also visible within field designators (see \ref{sub:opd})
referring to elements of record variables:
\begin{verbatim}
  rec = RECORD [(base)] [fields] END
  base = qid

  fields = field {; field}
  field = xids: type
  xids = xid{, xid}
\end{verbatim}
If a record type is exported, field identifiers that are to be visible outside the declaring
module must be marked (\emph{public fields}); unmarked ones are \emph{private fields}.

Record types are extensible, i.e. a record type can be defined as an extension of another
record type. In the examples above, \verb|CenterNode| (directly) extends \verb|Node|, which
is the (direct) base type of \verb|CenterNode|. More specifically, \verb|CenterNode|
extends \verb|Node| with the fields name and subnode.

\textbf{Definition}: A type \verb|T| \emph{extends} a type $T_0$, if it equals $T_0$, or
directly extends an extension of $T_0$. Conversely, a type $T_0$ is a \emph{base type} of
\verb|T|, if it equals \verb|T|, or is the direct base type of a base type of \verb|T|.

Examples:
\begin{verbatim}
  RECORD day, month, year: INT
  END
 
  RECORD
    name, firstname: ARRAY 32 OF CHAR;
    age: INT;
    salary: REAL
  END
\end{verbatim}

\subsubsection{POINTER}
Variables of a pointer type $P$ assume as values pointers to variables of some type $T$. It must be a
record type. The pointer type $P$ is said to be bound to $T$, and $T$ is the pointer base type of $P$.
Pointer types inherit the extension relation of their base types, if there is any. If a type $T$ is an
extension of $T_0$ and $P$ is a pointer type bound to $T$, then $P$ is also an extension of $P_0$, the
pointer type bound to $T_0$.
\begin{verbatim}
  ptr = POINTER TO type
\end{verbatim}
If a type \verb|P| is defined as \verb|POINTER TO T|, the identifier \verb|T| can be declared textually
following the declaration of \verb|P|, but (if so) it must lie within the same scope.

If $p$ is a variable of type \verb|P = POINTER TO T|, then a call of the predefined procedure
\verb|NEW(p)| has the following effect (see \ref{sub:ppro}): A variable of type \verb|T| is allocated
in free storage, and a pointer to it is assigned to $p$. This pointer $p$ is of type \verb|P| and the
referenced variable p\^{} is of type \verb|T|. Failure of allocation results in $p$ obtaining the value
\verb|NIL|. Any pointer variable may be assigned to \verb|NIL|, which points to no variable at all.

\subsubsection{PROC}
Variables of a procedure type \verb|T| have a procedure (or \verb|NIL|) as value. If a procedure \verb|P|
is assigned to a procedure variable of type \verb|T|, the (types of the) parameters of \verb|P|
must be the same as those indicated in the parameters of \verb|T|. The same holds for the result
type in the case of a function procedure (see \ref{sub:pars}). \verb|P| must not be declared local to
another procedure, and neither can it be a standard procedure.
\begin{verbatim}
  pro = PROC [params]
  fun = PROC [params]: qid
\end{verbatim}

\subsection{Variable}
\label{sub:var}
Variable declarations serve to introduce variables and associate them with identifiers that must be
unique within the given scope. They also serve to associate fixed data types with the variables.
\begin{verbatim}
  field = xids: type
\end{verbatim}
Variables whose identifiers appear in the same list are all of the same type.

Examples of variable declarations (see examples in \ref{sub:typ}):
\begin{verbatim}
  i, j, k: INT
     x, y: REAL
     p, q: BOOL
        s: SET
        f: Function
        a: ARRAY 100 OF REAL
        w: ARRAY 16 OF RECORD
                         ch: CHAR;
                         count: INT
                       END
        t: Tree
\end{verbatim}

\section{Expressions}
\label{sec:expr}
Constructs denoting rules of computation whereby constants and current values of variables
are combined to derive other values by the application of operators and function procedures.
Expressions consist of \emph{operands} and \emph{operators}, whose specific associations
may be changed by using parentheses.

\subsection{Operands}
\label{sub:opd}
With the exception of sets and literal constants, i.e. numbers and strings, operands are
denoted by designators. A \emph{designator} consists of an identifier referring to the
constant, variable, or procedure to be designated. This identifier may possibly be qualified
by module identifiers (see \ref{sec:DS} and \ref{sec:mod}), and it may be followed by
selectors, if the designated object is a structure element.

If \verb|A| designates an array, then \verb|A[E]| denotes that element of \verb|A| whose
index is the current value of the expression \verb|E|. The type of \verb|E| must be of
\verb|INT|. A designator of the form \verb|A[E1, E2, ..., En]| stands for \verb|A[E1][E2]...[En]|.
If $p$ designates a pointer variable, $p$\^{} denotes the variable which is referenced by
$p$. If $r$ designates a record, then $r.f$ denotes the field $f$ of $r$. If $p$ designates
a pointer, $p.f$ denotes the field $f$ of the record $p$\^{}, i.e. the dot implies
dereferencing and $p.f$ stands for $p$\^{}.$f$.

The type guard $v(T_0)$ asserts that $v$ is of type $T_0$, i.e. it aborts program execution,
if it is not of type $T_0$. The guard is applicable, if
\begin{enumerate}
  \item $T_0$ is an extension of the declared type $T$ of $v$, and
  \item $v$ is a variable parameter of record type, or $v$ is a pointer.
\end{enumerate}
\begin{verbatim}
   des = qid {sel}
   sel = . id | '[' exprs ']' | ^ | (qid)
   exprs = expr{, expr}
\end{verbatim}
If the designated object is a variable, then the designator refers to the variable's
current value. If the object is a procedure, a designator without parameter list refers to
that procedure. If it is followed by a (possibly empty) parameter list, the designator
implies an activation of the procedure and stands for the value resulting from its execution.
The (types of the) arguments must correspond to the parameters as specified
in the procedure's declaration (see \ref{sec:pro}).

Examples of designators (see examples in \ref{sub:var}):
\begin{verbatim}
  i                       (INT)
  a[i]                    (REAL)
  w[3].ch                 (CHAR)
  t.key                   (INT)
  t.left.right            (Tree)
  t(CenterNode).subnode   (Tree)
\end{verbatim}

\subsection{Operators}
\label{sub:op}
The syntax of expressions distinguishes between 4 classes of operators with different precedences
(binding strengths). The operator \~{} has the highest precedence, followed by multiplication
operators, addition operators, and relations. Operators of the same precedence associate from
left to right. For example, x-y-z stands for (x-y)-z.
\begin{verbatim}
  expr = exp [rel exp]
  rel  = = | # | < | <= | > | >= | IN | IS
  exp  = [sign] term {add term}
  add  = sign | OR
  term = factor {mul factor}
  mul  = * | / | DIV | MOD | &
  factor = number | string | NIL | TRUE | FALSE
           | set | pc | (expr) | ~factor
  set  = '{' [seg {, seg}] '}'
  seg  = expr[.. expr]
  args = ( [exprs] )
\end{verbatim}
The set ${m..n}$ denotes ${m, m+1, ... , n-1, n}$ if $m \le n$, else the empty set. The
available operators are listed in the following tables. In some instances, several different
operations are designated by the same operator symbol. In these cases, the actual operation
is identified by the type of the operands.

\subsubsection{Logical Operators}
\begin{table}[h!]
  \centering
  \begin{tabular}{c l}
    symbol & result \\\hline
    OR   & logical disjunction \\
    \&   & logical conjunction \\
    \~{} & negation
  \end{tabular}
\end{table}
These operators apply to BOOL operands and yield a BOOL result.
\begin{verbatim}
  p OR q  stands for  "if p then TRUE, else q"
  p  & q  stands for  "if p then q, else FALSE"
     ~ p  stands for  "not p"
\end{verbatim}

\subsubsection{Arithmetic Operators}
\begin{table}[h!]
  \centering
  \begin{tabular}{c l}
    symbol & result \\\hline
    +   & sum \\
    -   & difference \\
    *   & product \\
    /   & quotient \\
    DIV & int quotient \\
    MOD & modulus
  \end{tabular}
\end{table}
The operators +, -, *, and / apply to operands of numeric types. Both operands must be of
the same type, which is also the type of the result. When used as unary operators, -
denotes sign inversion and + denotes the identity operation.

The operators DIV and MOD apply to integer operands only. Let $q = x\text{ DIV }y$, and
$r = x\text{ MOD }y$.  Then quotient $q$ and remainder $r$ are defined by the equation:
\[ x = qy + r\text{  where }0 \le r < y \]

\subsubsection{Set Operators}
\begin{table}[h!]
  \centering
  \begin{tabular}{c l}
    symbol & result \\\hline
    + & union \\
    - & difference \\
    * & intersection \\
    / & symmetric set difference
  \end{tabular}
\end{table}
When used with a single operand of type SET, the minus sign denotes the set complement.

\subsubsection{Relations}
Relations are Boolean. The ordering relations \verb|<, <=, >, >=| apply to the numeric
types, \verb|CHAR|, and \verb|CHAR| arrays. The relations \verb|=| and \verb|#| also
apply to \verb|BOOL|, \verb|SET|, pointer and procedure types.
\begin{table}[h!]
  \centering
  \begin{tabular}{c l}
    symbol & relation \\\hline
    =  & equal \\
    \# & unequal \\
    <  & less \\
    <= & less or equal \\
    >  & greater \\
    >= & greater or equal \\
    IN & set membership \\
    IS & type test
  \end{tabular}
\end{table}

$x\text{ IN }s$ stands for "$x$ is an element of $s$". $x$ must be of \verb|INT|, and $s$
of \verb|SET|.

$v$\verb| IS T| stands for "$v$ is of type \verb|T|", which is called \emph{type test}.
It is applicable, if
\begin{enumerate}
  \item \verb|T| is an extension of the declared type $T_0$ of $v$, and
  \item $v$ is a variable parameter of record type or $v$ is a pointer.
\end{enumerate}
Assuming, for instance, that \verb|T| is an extension of $T_0$ and that $v$ is a designator
declared of $T_0$, then the test $v$\verb| IS T| determines whether the actually designated
variable is (not only a $T_0$, but also) a \verb|T|. The value of \verb|NIL IS T| is undefined.

Examples of expressions (refer to examples in \ref{sub:var}):
\begin{verbatim}
  1987               (INT)
  i DIV 3            (INT)
  ~p OR q            (BOOL)
  (i+j) * (i-j)      (INT)
  s - {8, 9, 13}     (SET)
  a[i+j] * a[i-j]    (REAL)
  (0<=i) & (i<100)   (BOOL)
  t.key = 0          (BOOL)
  k IN {i .. j-1}    (BOOL)
  t IS CenterNode    (BOOL)
\end{verbatim}

\section{Statements}
Denote actions. There are statements of:
\begin{description}
  \item[Elementary] Simplest, not composed of other ones:
  \begin{itemize}
    \item the assignment, and 
    \item procedure call.
  \end{itemize}
  \item[Structured] Composed of others, expressing sequencing and execution of:
  \begin{itemize}
    \item conditional,
    \item selective, and
    \item repetitive.
  \end{itemize}
  \item[Empty] No actions, relaxing punctuation rules in \ref{sub:ss}.
\end{description}
\begin{verbatim}
  s = [assign | pc | if | cases | while | repeat | for]
\end{verbatim}

\subsection{Statement Sequences}
\label{sub:ss}
Denote the sequence of actions specified by component statements, separated by semicolons:
\begin{verbatim}
  ss = s{; s}
\end{verbatim}

\subsection{Assignments}
Assign variables new values specified by the expression following the assignment operator,
\verb|:=|, read as "becomes":
\begin{verbatim}
  assign = des:=expr
\end{verbatim}

If a value parameter is structured (of array or record type), no assignment to it or its
elements is permitted. Neither may assignments be made to imported variables.

The type of the expression must be the same as of the designator. The following exceptions hold:
\begin{enumerate}
  \item The constant NIL can be assigned to variables of any pointer or procedure type.
  \item Strings can be assigned to any array of CHAR, provided the number of characters in
    the string is less than that of the array. (A null character is appended).
    Single-character strings can also be assigned to variables of type CHAR.
  \item In the case of records, the type of the source must be an extension of the one of
    the destination.
  \item An open array may be assigned to an array of equal base type.
\end{enumerate}

Examples of assignments (see examples in \ref{sub:var}):
\begin{verbatim}
  i := 0
  p := i = j
  x := FLT(i + 1)
  k := (i + j) DIV 2
  f := log2
  s := {2, 3, 5, 7, 11, 13}
  a[i] := (x+y) * (x-y)
  t.key := i
  w[i+1].ch := "A"
\end{verbatim}

\subsection{Procedure Calls}
A procedure call activates a procedure. It may contain arguments to be substituted in place
of their corresponding parameters defined in the procedure declaration (see \ref{sec:pro}).
The correspondence is established by their positions in the lists of arguments/parameters,
respectively. There are 2 kinds of parameters:
\begin{description}
  \item[variable] arguments must be designators denoting variables. If it designates an
    element of a structured variable, selectors are evaluated before procedure execution.
  \item[value] the corresponding argument must be an expression, evaluated prior to the
    procedure activation. The resulting value is assigned to the parameter constituting
    a local variable (see also \ref{sub:pars}).
\end{description}
\begin{verbatim}
  pc = des [args]
\end{verbatim}
Examples of procedure calls: (see \ref{sec:pro})
\begin{verbatim}
  ReadInt(i)
  WriteInt(2*j + 1, 6)
  INC(w[k].count)
\end{verbatim}

\subsection{IF}
Specify the conditional execution of statements guarded by preceding boolean expressions,
which is called \emph{guards}. They are evaluated in sequence of occurrence, until one is
\verb|TRUE|, whereafter its guarded statement sequence will be executed. If no guards
satisfied, the \verb|ELSE| one is executed (if there is).
\begin{verbatim}
  if = IF expr THEN ss
   {ELSIF expr THEN ss}
              [ELSE ss]
      END
\end{verbatim}
Example:
\begin{verbatim}
   IF (ch >= "A") & (ch <= "Z") THEN readIdentifier
ELSIF (ch >= "0") & (ch <= "9") THEN readNumber
ELSIF  ch  = 22X                THEN readString
  END
\end{verbatim}

\subsection{CASE}
Specify the selection and execution of a statement sequence according to the value of an
expression. First the case expression is evaluated, then the statement sequence is executed
whose case label list contains the obtained value. If the case expression is of \verb|INT|
or \verb|CHAR|, all labels must be integers or single-character strings, respectively.
\begin{verbatim}
  cases = CASE expr OF case {'|' case} END
  case  = [ranges: ss]
 
  ranges = range {, range}
  range  = label [.. label]
  label  = int | string | qid
\end{verbatim}
Example:
\begin{verbatim}
  CASE k OF 0: x := x + y
          | 1: x := x - y
          | 2: x := x * y
          | 3: x := x / y
  END
\end{verbatim}

The type $T$ of the case expression (case variable) may also be a record or pointer type.
Then the case labels must be extensions of $T$, and in the statements $S_i$ labelled by
$T_i$, the case variable is considered as of type $T_i$.  Example:
\begin{verbatim}
  TYPE R = RECORD     a: INT END;
      R0 = RECORD (R) b: INT END;
      R1 = RECORD (R) b: REAL END;
      R2 = RECORD (R) b: SET END;
       P = POINTER TO R;
      P0 = POINTER TO R0;
      P1 = POINTER TO R1;
      P2 = POINTER TO R2;
  VAR p: P;
 
  CASE p OF
    P0: p.b := 10  |
    P1: p.b := 2.5 |
    P2: p.b := {0, 2}
  END
\end{verbatim}

\subsection{WHILE}
Specify repetition: if any of boolean expressions (\emph{guards}) yields \verb|TRUE|, the
corresponding statement sequence will be executed. It ends while NONE yields \verb|TRUE|:
\begin{verbatim}
  while = WHILE expr DO ss
         {ELSIF expr DO ss} END
\end{verbatim}
Examples:
\begin{verbatim}
  WHILE j > 0 DO
    j := j DIV 2; i := i+1
  END
 
  WHILE (t # NIL) & (t.key # i) DO
    t := t.left
  END
 
  WHILE m > n DO m := m – n
  ELSIF n > m DO n := n – m
  END
\end{verbatim}

\subsection{REPEAT}
Specify repeated execution of a statement sequence until conditions satisfied:
\begin{verbatim}
  repeat = REPEAT ss UNTIL expr
\end{verbatim}
The sequence is executed at least once.

\subsection{FOR}
Specify repeated execution of a statement sequence for the \emph{control variable} be
progressed at the specified (integer) \verb|TO| value:
\begin{verbatim}
  for = FOR id := expr TO expr [BY expr] DO ss END
\end{verbatim}
\begin{verbatim}
  FOR v := beg TO end BY step DO ss END
\end{verbatim}
is equivalent to
\begin{verbatim}
  v := beg;
  WHILE v <= end DO ss; v := v + step END
\end{verbatim}
when $step > 0$. While $step < 0$, it is equivalent to
\begin{verbatim}
  v := beg;
  WHILE v >= end DO ss; v := v + step END
\end{verbatim}
\verb|v, beg, end, step| must all be of \verb|INT|. Moreover, the \verb|step| must NOT be
zero. Its default is 1, if not specified.

\section{Procedures}
\label{sec:pro}
Procedure declarations consist of:
\begin{itemize}
  \item a heading, specifies the
  \begin{itemize}
    \item procedure identifier,
    \item parameters, and
    \item result type (if any).
  \end{itemize}
  \item a body, contains
  \begin{itemize}
    \item declarations, and
    \item statements.
  \end{itemize}
\end{itemize}
A repeated procedure identifier following END at the end finalizes the whole declaration.

There are 2 kinds of procedures, namely:
\begin{enumerate}
  \item proper procedures, and
  \item function procedures.
\end{enumerate}
The latter are activated by a function designator as a constituent of an expression, and yield a result
that is an operand in the expression. Proper procedures are activated by a procedure call. A function
procedure is distinguished in the declaration by indication of the type of its result following the
parameter list. Its body must end with a RETURN clause which defines the result of the function procedure.

All constants, variables, types, and procedures declared within a procedure body are local to the
procedure. The values of local variables are undefined upon entry to the procedure. Since procedures
may be declared as local objects too, procedure declarations may be nested.

In addition to its parameters and locally declared objects, the objects declared globally are
also visible in the procedure.

The use of the procedure identifier in a call within its declaration implies recursive activation of the
procedure.
\begin{verbatim}
  proc = prop | func
  prop = PROC xid [params]; decls
         [BEGIN ss] END id
  func = PROC xid [params]: qid; decls
         [BEGIN ss] [RETURN expr] END id
  decls = [CONST {const;}]
          [TYPE {typdef;}]
          [VAR {field;}]
          {proc;}
\end{verbatim}

\subsection{Parameters}
\label{sub:pars}
Parameters are identifiers which denote arguments specified in the procedure call.
The correspondence between parameters and arguments is established when the procedure is
called. There are two kinds of parameters, namely value and variable parameters. A variable
parameter corresponds to an argument that is a variable, and it stands for that variable. A
value parameter corresponds to an argument that is an expression, and it stands for its
value, which cannot be changed by assignment. However, if a value parameter is of a basic type, it
represents a local variable to which the value of the actual expression is initially assigned.

The kind of a parameter is indicated in the parameter list: Variable parameters are denoted
by the symbol VAR and value parameters by the absence of a prefix.

A function procedure without parameters must have an empty parameter list. It must be called by a
function designator whose argument list is empty too.

Parameters are local to the procedure, i.e. their scope is the program text which constitutes
the procedure declaration.
\begin{verbatim}
  params = ( [param{; param}] )
  param = [VAR] id{, id}: {ARRAY OF} qid
\end{verbatim}

The type of each parameter is specified in the parameter list. For variable parameters, it
must be identical to the corresponding argument's type, except in the case of a record,
where it must be a base type of the corresponding argument's type.

If the parameter's type is specified as
\begin{verbatim}
  ARRAY OF T
\end{verbatim}
the parameter is said to be an open array, and the corresponding argument may be of
arbitrary length.

If a parameter specifies a procedure type, then the corresponding argument must be
either a procedure declared globally, or a variable (or parameter) of that procedure type. It cannot
be a predefined procedure. The result type of a procedure can be neither a record nor an array.

Examples of procedure declarations:
\begin{verbatim}
  PROC ReadInt(VAR x: INT);
    VAR i : INT; ch: CHAR;
  BEGIN i := 0; Read(ch);
    WHILE ("0" <= ch) & (ch <= "9") DO
      i := 10*i + (ORD(ch)-ORD("0")); Read(ch)
    END ;
    x := i
  END ReadInt
 
  PROC WriteInt(x: INT); (* 0 <= x < 10^5 *)
    VAR i: INT;
      buf: ARRAY 5 OF INT;
  BEGIN i := 0;
    REPEAT buf[i] := x MOD 10; x := x DIV 10;
           INC(i) UNTIL x = 0;
    REPEAT DEC(i); Write(CHR(buf[i] + ORD("0")))
                   UNTIL i = 0
  END WriteInt
 
  PROC log2(x: INT): INT;
    VAR y: INT; (* assume x>0 *)
  BEGIN y := 0;
    WHILE x > 1 DO x := x DIV 2; INC(y) END;
    RETURN y
  END log2
\end{verbatim}

\subsection{Predefined}
\label{sub:ppro}
The following table lists the predefined functions. Some are generic, i.e. they apply to
several types of operands. $v$ stands for a variable, $x$ and $n$ for expressions,
and \verb|T| for a type.
\begin{table}[h!]
  \centering
  \begin{tabular}{c c c l}
    Name     & ArgType & ResType & Function \\\hline
    ABS(x)   & numeric & type(x) & absolute value \\
    ODD(x)   & INT     & BOOL    & x MOD 2 = 1 \\
    LEN(v)   & array   & INT     & length \\
    LSL(x,n) & INT     & INT     & logical shift left \\
    ASR(x,n) & INT     & INT     & signed shift right \\
    ROR(x,n) & INT     & INT     & rotated right
  \end{tabular}
\end{table}

Type conversion functions:
\begin{table}[h!]
  \centering
  \begin{tabular}{c c c l}
    Name     & ArgType & ResType & Function \\\hline
    FLOOR(x) & REAL    & INT     & round down \\
    FLT(x)   & INT     & REAL    & identity \\
    ORD(x)   & CHAR |  & INT     & ordinal \# of \\
             & BOOL | SET \\
    CHR(x)   & INT     & CHAR    & character of \\
  \end{tabular}
\end{table}

Prop procedures:
\begin{table}[h!]
  \centering
  \begin{tabular}{c c l}
    Name      & ArgType  & Procedure \\\hline
    INC(v)    & INT      & v := v + 1 \\
    INC(v,n)  & INT      & v := v + n \\
    DEC(v)    & INT      & v := v - 1 \\
    DEC(v,n)  & INT      & v := v - n \\
    INCL(v,x) & SET,INT  & v := v + {x} \\
    EXCL(v,x) & SET,INT  & v := v - {x} \\
    NEW(v)    & pointer type & allocate v\^{} \\
    ASSERT(b) & BOOL     & abort if \~{}b \\
    PACK(x,n) & REAL,INT & pack into x \\
    UNPK(x,n) & REAL,INT & unpack x
  \end{tabular}
\end{table}

The \verb|FLOOR(x)| yields the largest int not greater than $x$:
\begin{verbatim}
  FLOOR(1.5) = 1        FLOOR(-1.5) = -2
\end{verbatim}

The parameter $n$ of \verb|PACK| represents the exponent of $x$. \verb|PACK(x, y)| is
equivalent to $x := x * 2^y$. \verb| UNPK| is the reverse operation. The resulting $x$
is normalized, such that $1.0 \le x < 2.0$.

\section{Modules}
\label{sec:mod}
A module is a collection of declarations of constants, types, variables, and procedures,
and a sequence of statements for the purpose of assigning initial values to the variables.
A module typically constitutes a text that is compilable as a unit.
\begin{verbatim}
  module = MODULE id;
             [IMPORT import{, import};]
             decls
           [BEGIN ss]
           END id.
  import = id[:= id]
\end{verbatim}

The import list specifies the modules of which the module is a client. If an identifier $x$
is exported from a module \verb|M|, and if \verb|M| is listed in a module's import list,
then $x$ is referred to as \verb|M.x|. If the form "\verb|M := M1|" is used in the import
list, an exported object $x$ declared within \verb|M1| is referenced in the importing module
as \verb|M.x|.

Identifiers that are to be visible in client modules, i.e. which are to be exported, must
be marked by an asterisk (export mark) in their declaration. Variables are always exported
in read-only mode.

The statement sequence following the symbol \verb|BEGIN| is executed when the module is added
to a system (loaded). Individual (parameterless) procedures can thereafter be activated from
the system, and these procedures serve as commands.  Example:
\begin{verbatim}
  MODULE Out; (*exported: Write, WriteInt, WriteLn*)
    IMPORT Texts, Oberon;
    VAR W: Texts.Writer;
 
    PROC Write*(ch: CHAR);
    BEGIN Texts.Write(W, ch)
    END Write;
 
    PROC WriteInt*(x, n: INT);
      VAR i: INT; a: ARRAY 16 OF CHAR;
    BEGIN i := 0;
      IF x < 0 THEN Texts.Write(W, "-"); x := -x END ;
      REPEAT a[i] := CHR(x MOD 10 + ORD("0"));
             x := x DIV 10; INC(i) UNTIL x = 0;
      REPEAT Texts.Write(W, " "); DEC(n) UNTIL n <= i;
      REPEAT DEC(i); Texts.Write(W, a[i]) UNTIL i = 0
    END WriteInt;
 
    PROC WriteLn*;
    BEGIN Texts.WriteLn(W);
      Texts.Append(Oberon.Log, W.buf)
    END WriteLn;
  BEGIN Texts.OpenWriter(W)
  END Out.
\end{verbatim}

\subsection{SYSTEM}
The optional module \verb|SYSTEM| contains definitions that are necessary to program low-level
operations referring directly to resources particular to a given computer and/or implementation.

These include for example facilities for accessing devices that are controlled by the
computer, and perhaps facilities to break the data type compatibility rules otherwise
imposed by the language definition.

There are 2 reasons for provoding facilites in \verb|SYSTEM|:
\begin{enumerate}
  \item Their value is implementation-dependent, i.e., it is not derivable from the language's
    definition, and
  \item they may corrupt a system (e.g. PUT).
\end{enumerate}
It is strongly recommended to restrict their use to specific low-level modules, as such
modules are inherently non-portable and not "type-safe". However, they are easily recognized
due to the identifier \verb|SYSTEM| appearing in the module's import lists. The subsequent
definitions are generally applicable. However, individual implementations may include in
their module \verb|SYSTEM| additional definitions that are particular to the specific,
underlying computer. In the following, $v$ stands for a variable, $x$, $a$, and $n$ for expressions.
\begin{table}[h!]
  \centering
  \begin{tabular}{c c c l}
    Name     & ArgType & ResType & Function \\\hline
    ADR(v)   & any     & INT & address of variable \\
    SIZE(T)  & any     & INT & size in bytes \\
    BIT(a,n) & INT & BOOL & bit n of mem[a]
  \end{tabular}
\end{table}
\begin{table}[h!]
  \centering
  \begin{tabular}{c c l}
    Name     & ArgType & Procedure \\\hline
    GET(a,v) & INT,& v := mem[a] \\
    PUT(a,x) &any basic& mem[a] := x \\\hline
    COPY(src,& INT & copy $n$ consecutive words\\
    ~~~~~~~~~~~~dst,n)&& ~~~~~~~~~from $src$ to $dst$
  \end{tabular}
\end{table}

The following are additional functions and procedures accepted by the compiler for
the RISC processor:
\begin{table}[h!]
  \centering
  \begin{tabular}{c c c l}
    Name     & ArgType & ResType & Function \\\hline
    VAL(T,n) & scalar  & T       & identity \\
    ADC(m,n) & INT & INT & add with carry C \\
    SBC(m,n) & INT & INT & subtract with carry C \\
    UML(m,n) & INT & INT & unsigned multiplication \\
    COND(n)  & INT & BOOL & IF Cond(n) THEN ...
  \end{tabular}
\end{table}
\begin{table}[h!]
  \centering
  \begin{tabular}{c c l}
    Name   & ArgType & Procedure \\\hline
    LED(n) & INT & display n on LEDs
  \end{tabular}
\end{table}
